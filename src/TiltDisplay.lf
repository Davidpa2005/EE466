/**
 * TiltDisplay.lf
 * Show pitch and roll (deg) computed from accelerometer on the 3pi+ 2040 LCD.
 *
 * Run:
 *   $ lfc src/TiltDisplay.lf
 *   $ picotool load -x bin/TiltDisplay.elf
 */

target C {
  platform: "RP2040",
  single-threaded: true
}

import Accelerometer from "lib/IMU.lf"
import Display from "lib/Display.lf"
import Tilt from "Tilt.lf"

main reactor {
  a = new Accelerometer()
  tilt = new Tilt()
  d = new Display()

  timer t(0, 250 msec)

  // Trigger a sensor sample
  reaction(t) -> a.trigger {=
    lf_set(a.trigger, true);
  =}

  // Feed x,y,z (g) into Tilt
  reaction(a.x, a.y, a.z) -> tilt.ax, tilt.ay, tilt.az {=
    lf_set(tilt.ax, a.x->value);
    lf_set(tilt.ay, a.y->value);
    lf_set(tilt.az, a.z->value);
  =}

  // Display pitch and roll
  reaction(tilt.pitch, tilt.roll) -> d.line0, d.line1 {=
    static char buf0[17];
    static char buf1[17];

    snprintf(buf0, 17, "P:%6.2f deg", tilt.pitch->value);
    snprintf(buf1, 17, "R:%6.2f deg", tilt.roll->value);

    lf_set(d.line0, buf0);
    lf_set(d.line1, buf1);
  =}
}